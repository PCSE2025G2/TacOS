/*
 * mp3Files.cmm : MP3ファイルの一覧
 */
#include <fs.hmm>
#include <util.hmm>
#include "appUtil.hmm"
#include "Files.hmm"

//-----------------------------------------------------------------------------
// MP3ファイルの一覧（ただし，最大12個）
//-----------------------------------------------------------------------------
// 12つのファイル名
char[][] fnames = array(12, 13);                     // 12345678.123
int numFile;                                        // ファイルの個数

char[] mp3Dir = "/MP3";
char[] path = array(18);                            // /MP3/12345678.123

char[] fnameToPath(char[] fname) {
  strCpy(path, mp3Dir);
  strCat(path, "/");
  strCat(path, fname);
  return path;
}

// /MP3 ディレクトリからファイルの一覧を作る（初期化）
public void mp3FilesInit() {
  int fd;                                           // ファイルディスクリプタ
  if ((fd=open(mp3Dir, READ_MODE))<0) {
    panic("panic:can't open MP3 Dir");
  }
  Dir dir;
  int i = 0;
  while (i<12 && (dir=readDir(fd, "MP3"))!=null) {   // 最大12つファイル名を読む
    strCpy(fnames[i], dir.name);
    i = i + 1;
  }
  numFile = i;
  close(fd);
}

// n 番目ファイル名を返す（1<=n && n<=5）
public char[] mp3FilesGetName(int n) {
  if (1<=n && n<=numFile) {
    return fnames[n-1];
  }
  return null;
}

// n 番目パス名を返す（1<=n && n<=5）
public char[] mp3FilesGetPath(int n) {
  if (1<=n && n<=numFile) {
    return fnameToPath(fnames[n-1]);
  }
  return null;
}
